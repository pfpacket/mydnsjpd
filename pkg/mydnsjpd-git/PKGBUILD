# Maintainer: Ryo Munakata <afpacket@gmail.com>
# Contributor: Ryo Munakata <afpacket@gmail.com>
pkgname=mydnsjpd-git
pkgver=20130401
pkgrel=1
pkgdesc="DNS record updater for MyDNS.jp"
arch=('i686' 'x86_64')
url="https://github.com/pfpacket/mydnsjpd"
license=('GPL2')
depends=('gcc>=4.6.0' 'boost-libs')
makedepends=('git' 'boost')
provides=('mydnsjpd')
install=mydnsjpd.install
source=('mydnsjpd.service')
md5sums=('2d601c4a9e44e0122f4cc016332ed714')

_gitroot="git://github.com/pfpacket/mydnsjpd.git"
_gitname="mydnsjpd"
# Your Boost library path
_boost_root="/usr"

build()
{
    cd "$srcdir"
    msg "Connecting to GIT server..."
    if [ -d "$_gitname" ] ; then
        cd "$_gitname" && git pull origin
        cd "$srcdir"
        msg "The local files are updated."
    else
        git clone --depth=1 "$_gitroot" "$_gitname"
    fi
    msg "GIT checkout done or server timeout"
    cd "$_gitname"
    make BOOST_ROOT="${_boost_root}"
}

package()
{
    cd "$srcdir/$_gitname"
    install -D -m600 ./mydnsjpd.conf "${pkgdir}/etc/mydnsjpd.conf"
    install -D -m755 ./mydnsjpd "${pkgdir}/usr/bin/mydnsjpd"
    install -D -m644 "${srcdir}/mydnsjpd.service" "${pkgdir}/usr/lib/systemd/system/mydnsjpd.service"
}
